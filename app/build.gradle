plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'

}

def majorVersion = 2
def appVersion = 1


def gitNumber(){
    def result = "git rev-list --count HEAD".execute([],project.rootDir).text.trim()
    if(result == null) throw new RuntimeException("Could not fetch the details!")
    return result.toString()
}

android {
    namespace 'com.sdapps.formula1fy'
    compileSdk 33

    defaultConfig {
        applicationId "com.sdapps.formula1fy"
        minSdk 22
        targetSdk 33
        versionCode 20
        versionName "${appVersion}.${majorVersion}.${gitNumber()}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE_PATH)
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
            storePassword RELEASE_STORE_PASSWORD
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "BASE_URL", "${BASE_ERGAST_API_URL}")
            signingConfig signingConfigs.release
        }

        debug {
            debuggable true
            buildConfigField("String", "BASE_URL", "${BASE_ERGAST_API_URL}")
            testCoverageEnabled = true

        }
    }

    viewBinding {
        enabled = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def versionName = variant.versionName

            outputFileName = "Formula1Fy_${versionName}_${gitNumber()}.apk"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    flavorDimensions 'firebase'
    productFlavors {
        firebase {
            dimension 'firebase'
            targetSdk 33
            minSdk 26
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation 'com.android.volley:volley:1.2.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'

    implementation platform('com.google.firebase:firebase-bom:32.1.0')
    implementation 'com.google.firebase:firebase-auth-ktx'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'


    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'


}